plugins {
    id 'java'
    id 'application'

    id 'org.owasp.dependencycheck' version '4.0.2'

    id 'com.github.ben-manes.versions' version '0.21.0'
    id 'se.patrikerdes.use-latest-versions' version '0.2.9'

    id 'checkstyle'
    id 'pmd'

    id 'jacoco'

    id 'com.github.hierynomus.license' version '0.15.0'
}

repositories {
    jcenter()
}

apply from: 'config/gradle/dependency-check.gradle'
apply from: 'config/gradle/dependency-updates.gradle'
apply from: 'config/gradle/checkstyle.gradle'
apply from: 'config/gradle/pmd.gradle'
apply from: 'config/gradle/jacoco.gradle'
apply from: 'config/gradle/license.gradle'

mainClassName = 'io.github.lbarnkow.jarb.JarbMain'

dependencies {
    // SHA-256 digest to hash passwords
    implementation 'commons-codec:commons-codec:1.12'

    // ToStringBuilder
    implementation 'org.apache.commons:commons-lang3:3.9'

    // Logging
    implementation 'org.slf4j:slf4j-simple:1.7.26'

    // javax.websocket implementation
    implementation 'org.glassfish.tyrus:tyrus-container-jdk-client:1.15'

    // REST client and JSON bindings for jaxrs (via jackson)
    implementation 'org.glassfish.jersey.core:jersey-client:2.28'
    implementation 'org.glassfish.jersey.inject:jersey-hk2:2.28'
    implementation 'org.glassfish.jersey.media:jersey-media-json-jackson:2.28'

    // YAML bindings for jackson
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.9.8'

    // JSR-330 dependency injection; implemented by Google guice
    implementation('com.google.inject:guice:4.2.2:no_aop') {
        exclude group: 'aopalliance', module: 'aopalliance'
    }

    // Use JUnit test framework
    testImplementation('org.junit.jupiter:junit-jupiter-api:5.4.2')
    testRuntimeOnly('org.junit.jupiter:junit-jupiter-engine:5.4.2')

    // Google Truth - Fluent assertions for Java
    testImplementation 'com.google.truth:truth:0.44'

    // Mocking Framework f√ºr unit tests
    testImplementation 'org.mockito:mockito-core:2.27.0'

    // Lombok Project to generate getters/setters/etc.
    compileOnly 'org.projectlombok:lombok:1.18.6'
    annotationProcessor 'org.projectlombok:lombok:1.18.6'
}

test {
    useJUnitPlatform()
    //testLogging.showStandardStreams = true
}
